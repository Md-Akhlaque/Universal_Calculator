apply plugin: 'com.android.application'


def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        config {
        }
        medyas {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "ml.medyas.wallbay"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        buildConfigField 'String', "PixabayApiKey", PIXABAY_KEY
        buildConfigField 'String', "PexelsApiKey", PEXELS_KEY
        buildConfigField 'String', "UnsplashApiKey", UNSPLASH_KEY
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName",  "wallbay-v"  + versionName )
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
//            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.medyas
        }
    }
    dataBinding {
        enabled = true
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:support-v4:28.0.0'
    // Admob - Firebase Analytics
    implementation 'com.google.android.gms:play-services-ads:17.1.2'
    implementation 'com.google.firebase:firebase-core:16.0.6'
    // Expresso Junit
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    def room_version = "1.1.1"
    def paging_version = "1.0.1"
    def lifecycle_version = "1.1.1"
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycle_version"
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    // optional - RxJava support for Room
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    implementation "android.arch.paging:runtime:$paging_version"
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.5.0'
    // JSON Converter
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    // PhotoEditor
    implementation 'ja.burhanrashid52:photoeditor:0.3.3'
    // Lottie - upgrade will result in manifest merge conflict
    implementation 'com.airbnb.android:lottie:2.7.0'
    // PhotoView - upgrade will result in manifest merge conflict
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
}


apply plugin: 'com.google.gms.google-services'



task generateReleaseBuild(dependsOn: [':app:bundleRelease', ':app:assembleRelease']) {
    doLast {
        println 'Release app built'
    }
}